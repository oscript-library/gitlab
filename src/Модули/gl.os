#Использовать 1connector
#Использовать logos

Перем СерверМодуля;
Перем ТокенМодуля;
Перем Лог;

#Область КореньAPI

Функция УстановитьСервер(Знач Сервер) Экспорт
	СерверМодуля = Сервер;
	Возврат Сервер;
КонецФункции

Функция УстановитьТокен(Знач Токен) Экспорт
	ТокенМодуля = Токен;
	Возврат Токен;
КонецФункции

// пайплайны

Функция ЗадачиПайплайна(Знач Проект, Знач Пайплайн) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/pipelines/%3/jobs", КореньAPI(), Проект, Пайплайн);
	Возврат ПолучитьВсеСтраницы(Запрос);
КонецФункции

Функция ЗадачиТекущегоПайплайна() Экспорт
	Возврат ЗадачиПайплайна(IDТекущегоПроекта(), IDТекущегоПайплайна());
КонецФункции

// мерж-реквесты

Функция МержРеквесты(Знач Проект, Знач Фильтр = "") Экспорт
	Запрос = СтрШаблон("%1/projects/%2/merge_requests", КореньAPI(), Проект);
	Запрос = ?(ПустаяСтрока(Фильтр), Запрос, СтрШаблон("%1?%2", Запрос, Фильтр));
	Возврат ПолучитьВсеСтраницы(Запрос);
КонецФункции

Функция ОткрытыеМержРеквесты(Знач Проект) Экспорт
	Возврат МержРеквесты(Проект, "state=opened");
КонецФункции

Функция МержРеквестыТекущегоПроекта(Знач Фильтр = "") Экспорт
	Возврат МержРеквесты(IDТекущегоПроекта(), Фильтр);
КонецФункции

Функция ОткрытыеМержРеквестыТекущегоПроекта() Экспорт
	Возврат ОткрытыеМержРеквесты(IDТекущегоПроекта());
КонецФункции

Функция МержРеквест(Знач Проект, Знач МержРеквест) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3", КореньAPI(), Проект, МержРеквест);
	Возврат КоннекторHTTP.Get(Запрос, , ПараметрыСоединения()).Json();
КонецФункции

Функция ТекущийМержРеквест() Экспорт
	Возврат МержРеквест(IDТекущегоПроекта(), IIDТекущегоМержРеквеста());
КонецФункции

Функция ИзмененияМержРеквеста(Знач Проект, Знач МержРеквест) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3/diffs", КореньAPI(), Проект, МержРеквест);
	Возврат ПолучитьВсеСтраницы(Запрос, 25); // больше 30 на моем гитлабе дает 500 ошибку
КонецФункции

Функция ИзмененияТекущегоМержРеквеста() Экспорт
	Возврат ИзмененияМержРеквеста(IDТекущегоПроекта(), IIDТекущегоМержРеквеста());
КонецФункции

Функция ИзмененныеФайлыМержРеквеста(Знач Проект, Знач МержРеквест) Экспорт
	
	Перем Результат, ИзменениеМР, Поле, ИмяФайла;

	ИзмененияМР = ИзмененияМержРеквеста(Проект, МержРеквест);

	Результат = Новый Массив;
	Для Каждого ИзменениеМР Из ИзмененияМР Цикл
		Для Каждого Поле Из СтрРазделить("old_path,new_path", ",") Цикл
			ИмяФайла = ИзменениеМР[Поле];
			Если Результат.Найти(ИмяФайла) = Неопределено Тогда
				Результат.Добавить(ИмяФайла);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция ИзмененныеФайлыТекущегоМержРеквеста() Экспорт
	Возврат ИзмененныеФайлыМержРеквеста(IDТекущегоПроекта(), IIDТекущегоМержРеквеста());
КонецФункции

Функция ОбсужденияМержРеквеста(Знач Проект, Знач МержРеквест) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3/discussions", КореньAPI(), Проект, МержРеквест);
	Возврат ПолучитьВсеСтраницы(Запрос, 25);
КонецФункции

Функция ОбсужденияТекущегоМержРеквеста() Экспорт
	Возврат ОбсужденияМержРеквеста(IDТекущегоПроекта(), IIDТекущегоМержРеквеста());
КонецФункции

Функция ОбсуждениеМержРеквеста(Знач Проект, Знач МержРеквест, Знач IDОбсуждения) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3/discussions/%4", КореньAPI(), Проект, МержРеквест, IDОбсуждения);
	Возврат КоннекторHTTP.Get(Запрос, , ПараметрыСоединения()).Json();
КонецФункции

Функция ОбсуждениеТекущегоМержРеквеста(Знач IDОбсуждения) Экспорт
	Возврат ОбсуждениеМержРеквеста(IDТекущегоПроекта(), IIDТекущегоМержРеквеста(), IDОбсуждения);
КонецФункции

Функция ДобавитьОбсуждениеМержРеквеста(Знач Проект, Знач МержРеквест, Знач Комментарий) Экспорт

	Обсуждение = Новый Структура;
	Обсуждение.Вставить("id", Проект);
	Обсуждение.Вставить("merge_request_iid", МержРеквест);
	Обсуждение.Вставить("body", Комментарий);

	Лог.Отладка("Подготовленная декорация:");
	Для Каждого Ключ Из Обсуждение Цикл
		Лог.Отладка("	%1: %2", Ключ.Ключ, Ключ.Значение);
	КонецЦикла;

	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3/discussions", КореньAPI(), Проект, МержРеквест);
	Возврат КоннекторHTTP.Post(Запрос, Обсуждение, , ПараметрыСоединения()).Json();
КонецФункции

Функция ДобавитьОбсуждениеТекущегоМержРеквеста(Знач Комментарий) Экспорт
	Возврат ДобавитьОбсуждениеМержРеквеста(IDТекущегоПроекта(), IIDТекущегоМержРеквеста(), Комментарий);
КонецФункции

Функция ЗакрытьОбсуждениеМержРеквеста(Знач Проект, Знач МержРеквест, Знач IDОбсуждения) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3/discussions/%4?resolved=true", КореньAPI(), Проект, МержРеквест, IDОбсуждения);
	Возврат КоннекторHTTP.Put(Запрос, , , ПараметрыСоединения()).Json();
КонецФункции

Функция ЗакрытьОбсуждениеТекущегоМержРеквеста(Знач IDОбсуждения) Экспорт
	Возврат ЗакрытьОбсуждениеМержРеквеста(IDТекущегоПроекта(), IIDТекущегоМержРеквеста(), IDОбсуждения);
КонецФункции

Функция ОтправитьРезультатВнешнейПроверки(Знач ПараметрыПроверки, Знач РезультатПроверки) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("id", ПараметрыПроверки["IDПроекта"]);
	Результат.Вставить("merge_request_iid", ПараметрыПроверки["IIDМержРеквеста"]);
	Результат.Вставить("sha", ПараметрыПроверки["Коммит"]);
	Результат.Вставить("external_status_check_id", ПараметрыПроверки["IDПроверки"]);
	Результат.Вставить("status", СтатусВнешнейПроверки(РезультатПроверки["Статус"]));

	Лог.Отладка("Подготовленный ответ:");
	Для Каждого Ключ Из Результат Цикл
		Лог.Отладка("	%1: %2", Ключ.Ключ, Ключ.Значение);
	КонецЦикла;

	Запрос = СтрШаблон("%1/projects/%2/merge_requests/%3/status_check_responses",
								КореньAPI(),
								ПараметрыПроверки["IDПроекта"],
								ПараметрыПроверки["IIDМержРеквеста"]);
								
	Возврат КоннекторHTTP.Post(Запрос, Результат, , ПараметрыСоединения()).Json();
КонецФункции

Функция ПараметрыВнешнейПроверки(Знач Запрос) Экспорт

	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ПутьПроекта", ПутьПроектаВнешнейПроверки(Запрос));
	ПараметрыПроверки.Вставить("IDПроекта", ПроектВнешнейПроверки(Запрос));
	ПараметрыПроверки.Вставить("IIDМержРеквеста", МержРеквестВнешнейПроверки(Запрос));
	ПараметрыПроверки.Вставить("СерверGitlab", СерверВнешнейПроверки(Запрос));
	ПараметрыПроверки.Вставить("Коммит", ХешКоммитаВнешнейПроверки(Запрос));
	ПараметрыПроверки.Вставить("IDПроверки", IDВнешнейПроверки(Запрос));

	Возврат ПараметрыПроверки;
КонецФункции

Функция СтатусВнешнейПроверки(Знач Статус) Экспорт
	Возврат ?(Статус, "passed", "failed");
КонецФункции

Функция СерверВнешнейПроверки(Знач Запрос) Экспорт
	
	URLРепо = РепозиторийВнешнейПроверки(Запрос);
	ПутьПроекта = ПутьПроектаВнешнейПроверки(Запрос);
	
	Возврат СтрЗаменить(URLРепо, ПутьПроекта, "");
КонецФункции

Функция ПроектВнешнейПроверки(Знач Запрос) Экспорт
	Возврат Запрос["project"]["id"];
КонецФункции

Функция МержРеквестВнешнейПроверки(Знач Запрос) Экспорт
	Возврат Запрос["object_attributes"]["iid"];
КонецФункции

Функция ПутьПроектаВнешнейПроверки(Знач Запрос) Экспорт
	Возврат Запрос["project"]["path_with_namespace"];
КонецФункции

Функция РепозиторийВнешнейПроверки(Знач Запрос) Экспорт
	Возврат Запрос["repository"]["homepage"];
КонецФункции

Функция ХешКоммитаВнешнейПроверки(Знач Запрос) Экспорт
	Возврат Запрос["object_attributes"]["last_commit"]["id"];
КонецФункции

Функция IDВнешнейПроверки(Знач Запрос) Экспорт
	Возврат Запрос["external_approval_rule"]["id"];
КонецФункции
// коммиты 

Функция Коммиты(Знач Проект, Знач От = "", Знач До = "") Экспорт
	
	Запрос = СтрШаблон("%1/projects/%2/repository/commits", КореньAPI(), Проект);
	Если ЗначениеЗаполнено(От) Или ЗначениеЗаполнено(До) Тогда
		Запрос = Запрос + СтрШаблон("?ref_name=%1...%2", От, До);
	КонецЕсли;

	Возврат ПолучитьВсеСтраницы(Запрос);	 
КонецФункции

Функция МержРеквестыКоммита(Знач Проект, Знач Коммит) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/repository/commits/%3/merge_requests", КореньAPI(), Проект, Коммит);
	Возврат КоннекторHTTP.Get(Запрос, , ПараметрыСоединения()).Json();
КонецФункции

// теги

Функция УстановитьТег(Знач ИмяТега, Знач Проект, Знач Коммит) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/repository/tags?tag_name=%3&ref=%4", КореньAPI(), Проект, ИмяТега, Коммит);
	Возврат КоннекторHTTP.POST(Запрос, , , ПараметрыСоединения());
КонецФункции

Функция УдалитьТег(Знач ИмяТега, Знач Проект) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/repository/tags/%3", КореньAPI(), Проект, ИмяТега);
	Возврат КоннекторHTTP.DELETE(Запрос, , , ПараметрыСоединения());
КонецФункции

Функция ПередвинутьТег(Знач ИмяТега, Знач Проект, Знач Коммит) Экспорт

	Попытка
		УдалитьТег(ИмяТега, Проект);
	Исключение
		Сообщить(СтрШаблон("Не смог удалить тег %1 в проекте %2:
		|%3", ИмяТега, Проект, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;

	Возврат УстановитьТег(ИмяТега, Проект, Коммит);
КонецФункции

// переменные

Функция IDТекущегоПроекта() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_PROJECT_ID");
КонецФункции

Функция ИмяТекущегоПроекта() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_PROJECT_NAME");
КонецФункции

Функция ПутьТекущегоПроекта() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_PROJECT_PATH");
КонецФункции

Функция IDТекущегоПайплайна() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_PIPELINE_ID");
КонецФункции

Функция IIDТекущегоМержРеквеста() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_MERGE_REQUEST_IID");
КонецФункции

Функция ОписаниеТекущегоМержРеквеста() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_MERGE_REQUEST_DESCRIPTION");
КонецФункции

Функция ИмяВеткиИсточникаТекущегоМержРеквеста() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_MERGE_REQUEST_BRANCH_NAME");
КонецФункции

Функция ИмяТекущейВетки() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_COMMIT_BRANCH");
КонецФункции

Функция ИмяВеткиПоУмолчанию() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_DEFAULT_BRANCH");
КонецФункции

Функция СсылкаНаПайплайн() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_PIPELINE_URL");
КонецФункции

Функция ТегиРаннера() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_RUNNER_TAGS");
КонецФункции

// разное

Функция ЗащищенныеВетки(Знач Проект) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/protected_branches", КореньAPI(), Проект);
	Возврат КоннекторHTTP.Get(Запрос, , ПараметрыСоединения()).Json();
КонецФункции

Функция ПроверкиСтатусов(Знач Проект) Экспорт
	Запрос = СтрШаблон("%1/projects/%2/external_status_checks", КореньAPI(), Проект);
	Возврат КоннекторHTTP.Get(Запрос, , ПараметрыСоединения()).Json();
КонецФункции

Функция МержРеквестыРелиза(Знач Проект, Знач ТегПредыдущегоРелиза, Знач ТегРелиза) Экспорт
	
	Результат = Новый Массив();
	Номера = Новый Массив();

	КоммитыРелиза = Коммиты(Проект, ТегПредыдущегоРелиза, ТегРелиза);
	Для Каждого Коммит Из КоммитыРелиза Цикл
		МержРеквестыКоммита = МержРеквестыКоммита(Проект, Коммит["id"]);
		Для Каждого МР Из МержРеквестыКоммита Цикл
			НомерМР = МР["iid"];
			Если Номера.Найти(НомерМР) = Неопределено Тогда
				Результат.Добавить(МР);
				Номера.Добавить(НомерМР);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Результат;
КонецФункции

Функция ЭтоМержРеквест() Экспорт
	Возврат ПолучитьПеременнуюСреды("CI_PIPELINE_SOURCE") = "merge_request_event";
КонецФункции

Функция ВыпуститьРелиз(Знач Релиз) Экспорт

	Артефакты = Новый Массив;
	Артефакты.Добавить(
		Новый Структура("name,url,link_type",
						"Поставка",
						Релиз.УРЛПоставки,
						"other"));

	Тело = Новый Структура;
	Тело.Вставить("id", Релиз.IDПроекта);
	Тело.Вставить("name", СтрШаблон("Релиз %1 %2", Релиз.ИмяПроекта, Релиз.НомерРелиза));
	Тело.Вставить("tag_name", Релиз.НомерРелиза);
	Тело.Вставить("released_at", Релиз.ДатаРелиза);
	Тело.Вставить("description", Релиз.Описание);
	Тело.Вставить("assets", Новый Структура("links", Артефакты));

	Запрос = СтрШаблон("%1/projects/%2/releases", КореньAPI(), Релиз.IDПроекта);
	Ответ = КоннекторHTTP.Post(Запрос, Тело,  ПараметрыСоединения());

	Лог.Отладка("Результат создания релиза %1 в проекте %2: %3", Релиз.ИмяПроекта, Релиз.НомерРелиза, Ответ.КодСостояния);

	Возврат АдресРелиза(Релиз.НомерРелиза);
КонецФункции

Функция АдресАртефактаДжобы(Знач IDДжобы, Знач ИмяАртефакта) Экспорт
	Возврат СтрШаблон("%1/-/jobs/%2/artifacts/raw/%3", URLПроекта(), IDДжобы, ИмяАртефакта);
КонецФункции

Функция АдресРелиза(Знач НомерРелиза) Экспорт
	Возврат СтрШаблон("%1/-/releases/%2", URLПроекта(), НомерРелиза);
КонецФункции

Функция URLПроекта() Экспорт
	Возврат СтрШаблон("%1://%2/%3",
					ПолучитьПеременнуюСреды("CI_SERVER_PROTOCOL"),
					ПолучитьПеременнуюСреды("CI_SERVER_HOST"),
					ПолучитьПеременнуюСреды("CI_PROJECT_PATH"));
КонецФункции

#КонецОбласти

#Область Вспомогательные_функции

Функция КореньAPI()
	Возврат СтрШаблон("%1/api/v4", СерверМодуля);
КонецФункции

Функция ПараметрыСоединения()
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", ТокенМодуля));

	Возврат Новый Структура("Заголовки,Таймаут", Заголовки, ТаймаутПоУмолчанию());
КонецФункции

Функция УровниДоступа()
	Уровни = Новый Соответствие;
	Уровни.Вставить("Maintainers", 40);
	Уровни.Вставить("Maintainers+Developers", 30);
	Уровни.Вставить("NoAccess", 0);

	Возврат Уровни;
КонецФункции

Функция ТаймаутПоУмолчанию()
	Возврат 30;
КонецФункции

// Пажинация

Функция ПолучитьВсеСтраницы(Знач Запрос, Знач КоличествоНаСтранице = 100,  Знач МаксимальноеКоличествоСтраниц = 100)

	Ответы = Новый Массив;

	НомерСтраницы = 1;
	Пока НомерСтраницы < МаксимальноеКоличествоСтраниц Цикл
		Ответ = КоннекторHTTP.Get(Запрос, Пажинация(КоличествоНаСтранице, НомерСтраницы), ПараметрыСоединения()).Json();
		Если Ответ.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		Для Каждого ЭлементОтвета Из Ответ Цикл
			 Ответы.Добавить(ЭлементОтвета);
		КонецЦикла;
		НомерСтраницы = НомерСтраницы + 1;
	КонецЦикла;

	Возврат Ответы;
КонецФункции

Функция Пажинация(Знач КоличествоНаСтранице, Знач НомерСтраницы)
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("page", НомерСтраницы);
	ПараметрыЗапроса.Вставить("per_page", КоличествоНаСтранице);
	
	Возврат ПараметрыЗапроса;
КонецФункции

#КонецОбласти

Лог = Логирование.ПолучитьЛог("oscript.lib.gitlab");
